import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
    // Adventure-bom snapshots required for velocity 3.3.0
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots"
    }
    maven { url "https://repo.not-null.co.uk/releases/" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
    compileOnly 'uk.co.notnull:PlatformDetection-velocity:2.2'

    annotationProcessor 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
}

group = 'uk.co.notnull'
version = '1.1-SNAPSHOT'

java.sourceCompatibility = JavaVersion.VERSION_21
java.targetCompatibility = JavaVersion.VERSION_21

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }
}
